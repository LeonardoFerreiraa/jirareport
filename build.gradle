plugins {
    id "org.springframework.boot" version "2.2.1.RELEASE"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"

    id "org.jetbrains.kotlin.jvm" version "1.3.50"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.3.50"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.50"

    id "io.gitlab.arturbosch.detekt" version "1.1.1"
}

apply from: "gradle/tasks/CreateMigration.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://repo.spring.io/milestone" }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-actuator"

    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.postgresql:postgresql"
    implementation "com.vladmihalcea:hibernate-types-52:2.7.1"
    implementation "org.flywaydb:flyway-core"

    implementation "org.springframework.boot:spring-boot-starter-security"

    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "com.github.ben-manes.caffeine:caffeine"

    implementation "io.github.openfeign:feign-httpclient"
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign"

    implementation "org.springframework.cloud:spring-cloud-starter-sleuth"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testImplementation "org.testcontainers:postgresql:1.12.3"

    testImplementation "com.github.javafaker:javafaker:1.0.1"
    testImplementation "io.rest-assured:rest-assured"
    testImplementation "com.github.tomakehurst:wiremock:2.25.1"

    testImplementation "org.springframework.security:spring-security-test"
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }

    testImplementation "org.junit.jupiter:junit-jupiter"

    testImplementation "io.mockk:mockk:1.9.3"

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.1.1"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Hoxton.RC2"
    }
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict", "-Xjvm-default=enable"]
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict", "-Xjvm-default=enable"]
        jvmTarget = "1.8"
    }
}

detekt {
    input = files(
            "src/main/kotlin",
            "src/test/kotlin"
    )
    config = files("$projectDir/detekt-config.yml")
}

task unitTest(type: Test) {
    useJUnitPlatform {
        includeTags "unit"
        excludeTags "integration"
    }
}
