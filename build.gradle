plugins {
    id "org.springframework.boot" version "2.1.5.RELEASE"

    id "org.jetbrains.kotlin.jvm" version "1.3.30"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.3.30"
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.30"

    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC14"
}

apply plugin: "io.spring.dependency-management"
apply from: "gradle/tasks/CreateMigration.gradle"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"

    implementation "org.springframework.boot:spring-boot-starter-cache"
    implementation "com.github.ben-manes.caffeine:caffeine"

    implementation "org.springframework.boot:spring-boot-starter-actuator"

    implementation "org.springframework.security:spring-security-jwt:1.0.10.RELEASE"

    implementation "org.apache.httpcomponents:httpclient:4.5.1"

    implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
    implementation "org.springframework.cloud:spring-cloud-starter-sleuth"

    implementation "org.postgresql:postgresql"

    implementation "com.vladmihalcea:hibernate-types-52:2.2.0"

    implementation "org.flywaydb:flyway-core"

    implementation 'io.reactivex.rxjava2:rxjava'

    implementation "org.springframework.boot:spring-boot-devtools"
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testImplementation "com.github.javafaker:javafaker:0.16"
    testImplementation "io.rest-assured:rest-assured"
    testImplementation "com.github.tomakehurst:wiremock:2.19.0"

    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.springframework.boot:spring-boot-starter-test"

    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.junit.jupiter:junit-jupiter-params"

    testImplementation "io.mockk:mockk:1.9.3"

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.0.0-RC14"
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE"
    }
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

detekt {
    input = files(
            "src/main/kotlin",
            "src/test/kotlin"
    )
    config = files("$projectDir/detekt-config.yml")
}

task unitTest(type: Test) {
    useJUnitPlatform {
        includeTags 'unit'
        excludeTags 'integration'
    }
}
